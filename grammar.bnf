%right 'in'
%right LOW
%right ']'
%nonassoc '>' '<' '<=' '>=' '==' '!=' '&&' '||' ident
%left '??'
%left 'not' 'escapeUri'

expr
  : json
  | kriti

kriti
  : '{{' path '}}'
  | '{{' operator '}}'
  | '{{' function '}}'
  | '(' kriti ')'
  | range
  | iff

kritiValue
  : path
  | operator
  | function
  | range
  | iff
  | json
  | '(' kritiValue ')'

path
  : path_vector

path_vector
  : ident path_tail
  | ident
  | ident '?'

path_tail
  : path_element
  | path_tail path_element

path_element
  : '.' ident
  | '?' '.' ident
  | '[' '\'' string '\'' ']'
  | '?' '[' '\'' string '\'' ']'
  | '[' int ']'
  | '?' '[' int ']'

operator
  : kritiValue '>' kritiValue
  | kritiValue '<' kritiValue
  | kritiValue '>=' kritiValue
  | kritiValue '<=' kritiValue
  | kritiValue '!=' kritiValue
  | kritiValue '==' kritiValue
  | kritiValue '&&' kritiValue
  | kritiValue '||' kritiValue
  | kritiValue 'in' kritiValue
  | kritiValue '??' kritiValue

function
  : 'escapeUri' kritiValue
  | 'not' kritiValue
  | ident kritiValue

range
  : '{{' 'range' mident ',' ident ':=' path_vector '}}' expr '{{' 'end' '}}'

mident
  : '_'
  | ident

iff
  : '{{' 'if' kritiValue '}}' expr '{{' 'else' '}}' expr '{{' 'end' '}}'

json
  : string_lit
  | num_lit
  | boolean
  | null
  | array
  | object

string_lit
  : 's"' string_template '"e'
  | 's"' '"e'

string_template
  : string_template '{{' template '}}'
  | string_template string
  | '{{' template '}}'
  | string

template
  : path
  | function
  | boolean
  | num_lit
  | operator

num_lit
  : number
  | int %prec LOW

boolean
  : 'true'
  | 'false'

null
  : 'null'

array
  : '[' list_elements ']'
  | '[' ']'

list_elements
  : expr
  | list_elements ',' expr

object
  : '{' object_fields '}'
  | '{' '}'

object_fields
  : object_field
  | object_fields ',' object_field

object_field
  : 's"' object_key '"e' ':' expr
  | 's"' '"e' ':' expr

object_key
  : object_key string
  | string